plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "com.google.protobuf" version "0.8.10"
    id "com.github.ben-manes.versions" version "0.22.0"
}

group 'org.athenian'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    maven { url 'https://jitpack.io' }
    maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
    mavenCentral()
    mavenLocal()
}

def kgaxVersion = '0.6.0'
def kotlinVersion = '1.3.50'
def coroutinesVersion = '1.3.0'
def grpcVersion = '1.23.0'
def protocVersion = '3.9.1'
def junitVersion = '4.12'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:${coroutinesVersion}"

    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"

    implementation "com.github.googleapis.gax-kotlin:kgax-grpc:${kgaxVersion}"

    testImplementation "junit:junit:${junitVersion}"
    testCompile "junit:junit:${junitVersion}"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/kotlin'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/client'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }

        client { artifact = "com.github.googleapis:gapic-generator-kotlin:${kgaxVersion}:core@jar" }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }

        all().each { task ->
            task.plugins {
                client {}
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-XXLanguage:+InlineClasses']
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}