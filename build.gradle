plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id "com.google.protobuf" version "0.8.8"
}

group 'org.athenian'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    maven { url 'https://jitpack.io' }
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'io.grpc', name: 'grpc-services', version: '1.20.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation 'com.github.googleapis.gax-kotlin:kgax-grpc:master-SNAPSHOT'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/main/kotlin'
            srcDirs 'build/generated/source/proto/main/client'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.7.1'
    }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.20.0" }

        client {
            artifact = 'com.github.googleapis:gapic-generator-kotlin:master-SNAPSHOT:core@jar'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }

        all().each { task ->
            task.plugins {
                client {}
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-XXLanguage:+InlineClasses']
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}